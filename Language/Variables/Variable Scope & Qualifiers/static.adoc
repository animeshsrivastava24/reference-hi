---
title: static
categories: [ "चर" ]
subCategories: [ "चर स्कोप और क्वालिफायर" ]
---

= Static


// अवलोकन अनुभाग शुरू होता है
[#अवलोकन]
--

[float]
=== विवरण
`static` कीवर्ड का उपयोग उन चर बनाने के लिए किया जाता है जो केवल एक फ़ंक्शन के लिए दिखाई देते हैं। हालाँकि, स्थानीय चर जो किसी फ़ंक्शन को कॉल करने और नष्ट करने के लिए मिलते हैं, के विपरीत, स्टैटिक चर फ़ंक्शन कॉल से परे बने रहते हैं, फ़ंक्शन कॉल के लिए अपने डेटा को संरक्षित करते हैं।

स्थिर के रूप में घोषित चर केवल पहली बार एक समारोह कहा जाता है बनाया और शुरू किया जाएगा।
[%hardbreaks]

--
// ओवरव्यू अनुभाग अंत




// कैसे उपयोग करें खंड की शुरुआत
[#कैसेउपयोगकरें]
--

[float]
=== उदाहरण कोड
// वर्णन करें कि उदाहरण कोड क्या है और संबंधित कोड जोड़ें   ►►►►► यह खंड अनिवार्य है ◄◄◄◄◄


[source,arduino]
----
/* RandomWalk
  Paul Badger 2007
  RandomWalk दो अंत बिंदुओं के बीच बेतरतीब ढंग से ऊपर और नीचे 
  घूमता है| एक लूप में अधिकतम चाल को पैरामीटर "stepsize" 
  द्वारा नियंत्रित किया जाता है।
  एक स्थिर चर एक यादृच्छिक राशि ऊपर और नीचे ले जाया जाता है।
  इस तकनीक को "pink noise" और "drunken walk" के रूप में भी जाना जाता है।
*/

#define randomWalkLowRange -20
#define randomWalkHighRange 20
int stepsize;

int thisTime;

void setup() {
  Serial.begin(9600);
}

void loop() {
  //  test randomWalk function
  stepsize = 5;
  thisTime = randomWalk(stepsize);
  Serial.println(thisTime);
  delay(10);
}

int randomWalk(int moveSize) {
  static int place; // रैंडम वॉक में मूल्य को स्टोर करने के लिए वैरिएबल - स्टैटिक घोषित किया गया ताकि वह स्टोर हो सके
  // फ़ंक्शन कॉल के बीच का मान, लेकिन कोई भी अन्य फ़ंक्शन इसका मान नहीं बदल सकता है

  place = place + (random(-moveSize, moveSize + 1));

  if (place < randomWalkLowRange) {                               // निचली और ऊपरी सीमा की जाँच करें
    place = randomWalkLowRange + (randomWalkLowRange - place);    // सकारात्मक दिशा में वापस संख्या को प्रतिबिंबित करें
  }
  else if (place > randomWalkHighRange) {
    place = randomWalkHighRange - (place - randomWalkHighRange);  // नकारात्मक दिशा में वापस संख्या को प्रतिबिंबित करें
  }

  return place;
}
----
[%hardbreaks]


--
// कैसे उपयोग करें खंड का अंत


// यह भी देखे खंड
[#यह_भी_देखे]
--

[float]
=== यह भी देखे

--
// यह भी देखे खंड का अंत
